class Node {
    int item         moves_as isLocal(this) ? B : readonly;
    Node next        moves_as isLocal(this) ? B : readonly;

    ghost Seq<int> spec moves_as E;

    invariant this.next == null ==> this.spec == SeqUnit(this.item);
    invariant this.next != null ==> this.spec == SeqConcat(SeqUnit(this.item), this.next.spec);

    Node(int item, Node next) {
      this.item = item;
      this.next = next;
      this.spec = this.next == null ? SeqUnit(this.item) : SeqConcat(SeqUnit(this.item), this.next.spec);
    }
}

class Stack {

  Node head moves_as isLocal(this) ? thread_local : guarded_by this;

  ghost Seq<int> spec moves_as E;

  invariant this.head == null ==> this.spec == SeqEmpty<int>();
  invariant this.head != null ==> this.spec == this.head.spec;

  modifies this;
  ensures this.spec == SeqEmpty<int>();
  public Stack() {
    this.head = null;
  }

  modifies this;
  ensures this.spec == [item] ++ old(this.spec);
  public void push(int item) {
    acquire(this);
    Node node = new Node(item, this.head);
    this.head = node;
    this.spec = [item] ++ this.spec;
    release(this);
  }

  modifies this;
  ensures old(this.spec) == [$result] ++ this.spec;
  public int pop() {
    acquire(this);
    while (this.head == null) 
      invariant holds(this);
    {
      release(this);
      yield;
      acquire(this);
    }
    int value = this.head.item;
    this.head = this.head.next;
    this.spec = this.head.spec;
    release(this);
    return value;
  }
}
